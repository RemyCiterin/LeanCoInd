import Mathlib.Order.OmegaCompletePartialOrder
import CoInd.Tactic
import CoInd.Kahn2

open OmegaCompletePartialOrder

-- define projections, map and lift operations over Pi types
namespace Pi

variable {Î±: Type uâ‚} {Î±': Type uâ‚‚}
variable {Î²: Î± â†’ Type uâ‚ƒ} [(a : Î±) â†’ OmegaCompletePartialOrder (Î² a)]
variable {Î³: Î± â†’ Type uâ‚„} [(a : Î±) â†’ OmegaCompletePartialOrder (Î³ a)]

@[simps! apply]
def OmegaCompletePartialOrder.proj (a: Î±) : ((a: Î±) â†’ Î² a) â†’ğ’„ (Î² a) where
  toFun p := p a
  monotone' := Î» _ _ hâ‚ => hâ‚ a
  cont _ := rfl

@[simps! apply]
def OmegaCompletePartialOrder.map (f:(a: Î±) â†’ Î² a â†’ğ’„ Î³ a) : (âˆ€ a, Î² a) â†’ğ’„ (âˆ€ a, Î³ a) where
  toFun p a := f a (p a)
  monotone' := Î» x y hâ‚ a => (f a).monotone' (hâ‚ a)
  cont := by
    intro h c
    funext a
    apply (f a).cont
    apply (f a).monotone'

@[simps! apply]
def OmegaCompletePartialOrder.lift (f: Î±' â†’ Î±) : (âˆ€ a, Î² a) â†’ğ’„ (âˆ€ a, Î² (f a)) where
  toFun p a := p (f a)
  monotone' _ _ hâ‚ _ := hâ‚ _
  cont _ := rfl

example (f: Î±' â†’ Î±) (p: âˆ€ a, Î² a) (a: Î±') :
  OmegaCompletePartialOrder.lift f p a = p (f a) := by
  simp

end Pi

@[simp]
def OmegaCompletePartialOrder.Fix.IterFun
  {Î±: Type u} [OmegaCompletePartialOrder Î±] [OrderBot Î±]
  (f: Î± â†’ Î±) : â„• â†’ Î±
| n+1 => f (IterFun f n)
| 0 => âŠ¥

def OmegaCompletePartialOrder.Fix.IterFun_le_succ
  {Î±: Type u} [OmegaCompletePartialOrder Î±] [OrderBot Î±]
  (f: Î± â†’o Î±) (n: â„•) : IterFun f n â‰¤ IterFun f (n+1) := by
  induction n with
  | zero =>
    apply bot_le
  | succ n hâ‚ =>
    apply f.monotone'
    exact hâ‚

def OmegaCompletePartialOrder.Fix.IterFun_mono
  {Î±: Type u} [OmegaCompletePartialOrder Î±] [OrderBot Î±]
  (f: Î± â†’o Î±) : Monotone (IterFun f) := by
  intro a b hâ‚
  induction hâ‚ with
  | refl =>
    apply le_refl
  | step _ hâ‚‚ =>
    apply hâ‚‚.trans
    apply IterFun_le_succ

@[simps! coe]
def OmegaCompletePartialOrder.Fix.Iter
  {Î±: Type u} [OmegaCompletePartialOrder Î±] [OrderBot Î±]
  (f: Î± â†’o Î±) : Chain Î± where
  toFun := IterFun f
  monotone' := IterFun_mono f

def OmegaCompletePartialOrder.Fix
  {Î±: Type u} [OmegaCompletePartialOrder Î±] [OrderBot Î±]
  (f: Î± â†’o Î±) : Î± := Ï‰Sup (Fix.Iter f)

-- fixed point of a continuous function
def OmegaCompletePartialOrder.FixCont
  {Î±: Type u} [OmegaCompletePartialOrder Î±] [OrderBot Î±]
  (f: Î± â†’ğ’„ Î±) : Î± := Ï‰Sup (Fix.Iter f)

namespace OmegaCompletePartialOrder.Fix
variable {Î±: Type u} [OmegaCompletePartialOrder Î±] [OrderBot Î±]

def unfold_mono (f: Î± â†’o Î±) : Fix f â‰¤ f (Fix f) := by
  apply Ï‰Sup_le
  intro n
  cases n with
  | zero =>
    apply bot_le
  | succ n =>
    apply f.monotone'
    apply le_Ï‰Sup (Iter f)

def unfold_cont (f: Î± â†’ğ’„ Î±) : Fix f = f (Fix f) := by
  suffices Fix f = Ï‰Sup ((Iter f).map f.toOrderHom) by
    conv =>
      lhs
      rw [this, â†f.cont]
      rfl
  apply le_antisymm
  . apply Ï‰Sup_le
    intro n
    apply LE.le.trans _ (le_Ï‰Sup _ n)
    simp only [Chain.map_coe, Function.comp_apply]
    apply (Iter f.toOrderHom).monotone' (show n â‰¤ n+1 by simp)
  . apply Ï‰Sup_le
    intro n
    apply le_Ï‰Sup (Iter f.toOrderHom) (n+1)

end OmegaCompletePartialOrder.Fix



namespace OmegaCompletePartialOrder.Chain
variable {Î±: Type u} [OmegaCompletePartialOrder Î±]

def filter (c: Chain Î±) (U: â„• â†’o â„•) : Chain Î± := c.comp U

-- ensure that an increasing seauence `c` filtred by `U` have a supremum less than
-- the supremum of `c`
def Ï‰Sup_filter (c: Chain Î±) (U: â„• â†’o â„•) : Ï‰Sup (filter c U) â‰¤ Ï‰Sup c := by
  apply Ï‰Sup_le
  intro n
  apply le_Ï‰Sup c (U n)

-- ensure that if an increasing sequance `c` is filtred with an injective function `U`, then
-- the supremum of `c` and `c.filter u` are equals
def Ï‰Sup_filter_inj (c: Chain Î±) (U: â„• â†’o â„•) (inj: Function.Injective U) : Ï‰Sup (c.filter U) = Ï‰Sup c := by
  apply le_antisymm
  . apply Ï‰Sup_filter
  . apply Ï‰Sup_le
    intro n
    have : âˆ€ n, n â‰¤ U n := by
      intro n
      induction n with
      | zero =>
        apply Nat.zero_le
      | succ n h =>
        have := Nat.succ_le_succ h
        apply this.trans
        have := @Nat.lt_iff_le_and_ne (U n) (U (.succ n))
        apply this.mpr
        constructor
        . apply U.monotone
          simp_arith
        . intro h
          specialize inj h
          cases inj
    apply le_trans _ (le_Ï‰Sup (filter c U) n)
    apply c.monotone'
    apply this

end OmegaCompletePartialOrder.Chain

def OmegaCompletePartialOrder.IsAdmissible {Î±: Type u} [OmegaCompletePartialOrder Î±] (S: Set Î±) :=
  âˆ€ (c: Chain Î±), (âˆ€ n, S (c n)) â†’ S (Ï‰Sup c)

structure OmegaCompletePartialOrder.Admissible
  (Î±: Type u) [OmegaCompletePartialOrder Î±] [OrderBot Î±] where
  toSet : Set Î±
  admissible': IsAdmissible toSet
  contain_bot: âŠ¥ âˆˆ toSet

attribute [coe] Admissible.toSet

namespace OmegaCompletePartialOrder.Admissible
variable {Î±: Type u} [OmegaCompletePartialOrder Î±] [OrderBot Î±]

instance instFunLike : FunLike (Admissible Î±) Î± (Î» _ => Prop) where
  coe := toSet
  coe_injective' := by
    intro a b hâ‚
    cases a
    cases b
    rw [Admissible.mk.injEq]
    exact hâ‚

instance : CoeFun (Admissible Î±) fun _ => Î± â†’ Prop := âŸ¨toSetâŸ©

instance : Membership Î± (Admissible Î±) :=
  âŸ¨Î» x s => s xâŸ©

@[refinment_type] def admissible (p: Admissible Î±) (c: Chain Î±) :
  (âˆ€ n, c n âˆˆ p) â†’ Ï‰Sup c âˆˆ p := p.admissible' c

-- Conjunction of two properties
def And (lhs rhs: Admissible Î±) : Admissible Î± where
  toSet x := x âˆˆ lhs âˆ§ x âˆˆ rhs

  admissible' := by
    intro c hâ‚
    constructor
    . apply lhs.admissible'
      intro n
      specialize hâ‚ n
      exact hâ‚.left
    . apply rhs.admissible'
      intro n
      specialize hâ‚ n
      exact hâ‚.right

  contain_bot := by
    constructor
    . exact lhs.contain_bot
    . exact rhs.contain_bot

@[refinment_type] def And.intro (lhs rhs: Admissible Î±) (x: Î±) :
  x âˆˆ lhs â†’ x âˆˆ rhs â†’ x âˆˆ And lhs rhs := by
  intro a v
  constructor
  <;> assumption

def Or.prop (p: Admissible Î±) (c: Chain Î±) (n: Nat) (m: Nat) : Prop := p (c (n+m))

/-
Define an increasing and injective sequence such that if `p` hold infinitly many times
in `c`, then `p` hold for each elements of `sequence p c`
-/
noncomputable def Or.sequence (p: Admissible Î±) (c: Chain Î±) : Nat â†’ Nat
| n+1 =>
  let m := sequence p c n
  (m+1) + Classical.epsilon (prop p c (m + 1))
| 0 => Classical.epsilon (prop p c 0)

theorem Or.sequence.strict_mono' (p: Admissible Î±) (c: Chain Î±) (n: â„•) :
  sequence p c n < sequence p c (n+1) := by
  rw [sequence]
  simp_arith

theorem Or.sequence.strict_mono (p: Admissible Î±) (c: Chain Î±) (n m: â„•) (hâ‚: n < m) :
  sequence p c n < sequence p c m := by
  induction hâ‚ with
  | refl =>
    apply strict_mono'
  | step _ hâ‚‚ =>
    apply hâ‚‚.trans
    apply strict_mono'

theorem Or.sequence.inj (p: Admissible Î±) (c: Chain Î±) : Function.Injective (sequence p c) := by
  intro a b hâ‚
  cases Nat.le_or_le a b with
  | inl hâ‚‚ =>
    cases le_iff_lt_or_eq.mp hâ‚‚ with
    | inl hâ‚ƒ =>
      have := strict_mono p c a b hâ‚ƒ
      rw [hâ‚] at this
      simp at this
    | inr hâ‚ƒ =>
      assumption
  | inr hâ‚‚ =>
    cases le_iff_lt_or_eq.mp hâ‚‚ with
    | inl hâ‚ƒ =>
      have := strict_mono p c b a hâ‚ƒ
      rw [hâ‚] at this
      simp at this
    | inr hâ‚ƒ =>
      cases hâ‚ƒ
      rfl

noncomputable def Or.sequence' (p: Admissible Î±) (c: Chain Î±) : â„• â†’o â„• where
  toFun := sequence p c
  monotone' := by
    intro a b hâ‚
    induction hâ‚ with
    | refl =>
      apply le_refl
    | step _ hâ‚‚ =>
      apply hâ‚‚.trans
      simp_arith [sequence]

def Or.sequence_spec (p: Admissible Î±) (c: Chain Î±) :
  (âˆ€ n, âˆƒ m, p (c (n + m))) â†’ âˆ€ n, p (c.filter (Or.sequence' p c) n) := by
  intro hâ‚ n
  cases n with
  | zero =>
    simp [Chain.filter, sequence', sequence, prop]
    apply @Classical.epsilon_spec _ (Î» m => p (c m))
    specialize hâ‚ 0
    conv at hâ‚ =>
      rhs; intro m
      rw [Nat.zero_add]
      rfl
    assumption
  | succ m =>
    simp [Chain.filter, sequence', sequence, prop]
    apply @Classical.epsilon_spec _ (Î» m' => p <| c <| sequence p c m + 1 + m')
    apply hâ‚

def Or.pigeonhole (p q: Admissible Î±) (c: Chain Î±) (hâ‚: âˆ€ n, p (c n) âˆ¨ q (c n)) :
  (âˆ€ n, âˆƒ m, p (c (n+m))) âˆ¨ (âˆ€ n, âˆƒ m, q (c (n+m))) := by
  by_cases (âˆ€ n, âˆƒ m, p (c (n+m)))
  . apply Or.inl h
  . apply Or.inr
    intro n
    conv at h =>
      rw [not_forall]
      rhs; intro k
      rw [not_exists]
      rfl
    have âŸ¨k, hâŸ© := h
    specialize h n
    rw [add_comm] at h
    specialize hâ‚ (n+k)
    exists k
    simp only [h, false_or] at hâ‚
    assumption

/- Disjunction of two admissible properties -/
def Or (lhs rhs: Admissible Î±) : Admissible Î± where
  toSet x := x âˆˆ lhs âˆ¨ x âˆˆ rhs

  contain_bot := by
    apply Or.inl
    exact lhs.contain_bot

  admissible' := by
    intro c hâ‚

    let lhsS := Or.sequence' lhs c
    let rhsS := Or.sequence' rhs c
    let lhsC := c.filter lhsS
    let rhsC := c.filter rhsS
    have lhsI : Ï‰Sup lhsC = Ï‰Sup c := Chain.Ï‰Sup_filter_inj c lhsS (Or.sequence.inj _ _)
    have rhsI : Ï‰Sup rhsC = Ï‰Sup c := Chain.Ï‰Sup_filter_inj c rhsS (Or.sequence.inj _ _)
    conv =>
      congr
      . rw [â†lhsI]
      . rw [â†rhsI]
    cases Or.pigeonhole lhs rhs c hâ‚ with
    | inl hâ‚‚ =>
      apply Or.inl
      apply lhs.admissible'
      apply Or.sequence_spec lhs c hâ‚‚
    | inr hâ‚‚ =>
      apply Or.inr
      apply rhs.admissible'
      apply Or.sequence_spec rhs c hâ‚‚


def Forall {Î²: Type v} (p: Î² â†’ Admissible Î±) : Admissible Î± where
  toSet x := âˆ€ y, x âˆˆ p y
  contain_bot := by
    intro y
    apply (p y).contain_bot
  admissible' := by
    intro c hâ‚ y
    apply (p y).admissible'
    intro n
    apply hâ‚

@[refinment_type] def contain_bot' (p: Admissible Î±) : âŠ¥ âˆˆ p := p.contain_bot

-- If a proposition `p` is admissible then if is enough to show that `p` is stable
-- by `f` to show that `Fix f` ensure `p`
@[refinment_type] def Fix_thm (p: Admissible Î±) (f: Î± â†’o Î±) (hyp: âˆ€ x, x âˆˆ p â†’ f x âˆˆ p) : Fix f âˆˆ p := by
  apply p.admissible' (Fix.Iter f)
  intro n
  induction n with
  | zero =>
    apply p.contain_bot
  | succ n hâ‚ =>
    exact hyp (Fix.Iter f n) hâ‚


end OmegaCompletePartialOrder.Admissible
